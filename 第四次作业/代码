PFont font;

String letters = "To be or not to be is a question";

float x = 0;
float y = 0;
float stepSize = 5.0;
float fontSizeMin = 8;
float angleDistortion = 0.25;

int counter = 5;

void setup() {
  // use full screen size
  fullScreen();
  background(0,0,128);
  cursor(CROSS);
  
  font = loadFont("TimesNewRomanPS-BoldItalicMT-120.vlw");
  textFont(font);
  textAlign(LEFT);
  fill(0,0,146);
}

void draw() {
  fill(255);
  textAlign(LEFT);
  textFont(font);
  
  if (mousePressed) {
    float d = dist(x, y, mouseX, mouseY);
    textSize(fontSizeMin + d / 3);
    char newLetter = letters.charAt(counter);
    stepSize = textWidth(newLetter);

    if (d > stepSize) {
      float angle = atan2(mouseY - y, mouseX - x);

      pushMatrix();
      translate(x, y);
      rotate(angle + random(angleDistortion));
      text(newLetter, 0, 0);
      popMatrix();

      counter++;
      if (counter >= letters.length()) counter = 0;

      x = x + cos(angle) * stepSize;
      y = y + sin(angle) * stepSize;
    }
  }
}

void mousePressed() {
  x = mouseX;
  y = mouseY;
}

void keyReleased() {
  if(key == 's' || key == 'S')
    save("goodworks//mywork"+year()+"_"+month()+"_"+day()
         +"_"+hour()+"_"+minute()+"_"+second()+".png");
  if (keyCode == DELETE || keyCode == BACKSPACE) background(0,0,128);
}

void keyPressed() {

  if (keyCode == UP) angleDistortion += 0.3;
  if (keyCode == DOWN) angleDistortion -= 0.5;
}
